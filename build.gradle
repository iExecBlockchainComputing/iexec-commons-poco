plugins {
    id 'java-library'
    id 'jacoco'
    id 'org.sonarqube' version '3.3'
    id 'maven-publish'
    id 'io.freefair.lombok' version '8.2.2'
}

group = 'com.iexec.commons'

if (!project.hasProperty('gitBranch')) {
    ext.gitBranch = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
}

if (gitBranch != 'main' && gitBranch != 'master' && ! (gitBranch ==~ '(release|hotfix|support)/.*')) {
    version += '-NEXT-SNAPSHOT'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://docker-regis-adm.iex.ec/repository/maven-public/'
        credentials {
            username nexusUser
            password nexusPassword
        }
    }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation platform('org.springframework.boot:spring-boot-dependencies:2.7.14')

    // web3j
    api 'org.web3j:core:4.9.7'

    // apache commons.lang3
    implementation 'org.apache.commons:commons-lang3'

    // multiaddresses (IPFS)
    implementation 'com.github.multiformats:java-multiaddr:v1.4.6'
    
    // jaxb required with Java 11 DataTypeConverter in BytesUtils
    implementation 'javax.xml.bind:jaxb-api'
    
    // failsafe
    implementation 'net.jodah:failsafe:2.4.4'
    //TODO Migrate from Awaitility to Failsafe everywhere
    implementation 'org.awaitility:awaitility'
    
    // javax annotations like PostConstruct
    implementation 'javax.annotation:javax.annotation-api'

    // tests
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.testcontainers:junit-jupiter:1.17.2'

    // spring-test for ReflectionTestUtils
    testImplementation 'org.springframework:spring-test'

    // logback
    testRuntimeOnly 'ch.qos.logback:logback-classic'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(Test).configureEach {
    finalizedBy jacocoTestReport
    useJUnitPlatform()
}

test {
    doFirst {
        exec {
            commandLine 'docker', 'compose', 'pull', '-q'
        }
    }
    reports {
        junitXml.required = true
        html.required = true
    }
}

tasks.register('itest') {
    group 'Verification'
    description 'Runs the integration tests.'
}

// sonarqube code coverage requires jacoco XML report
jacocoTestReport {
    reports {
        xml.required = true
    }
}
tasks.sonarqube.dependsOn tasks.jacocoTestReport

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username nexusUser
                password nexusPassword
            }
            url = project.hasProperty('nexusUrl') ? nexusUrl : ''
        }
    }
}
